horario('Path:38',461,[63780,63900]).
horario('Path:39',462,[74520,74640]).
horario('Path:38',463,[67800,67900]).
horario('Path:39',464,[85800,85920]).
horario('Path:38',465,[69600,69720]).
horario('Path:39',466,[76800,76920]).
horario('Path:38',467,[67800,67920]).
horario('Path:38',468,[69600,69720]).
horario('Path:11',201,[27420,27900,28140,28380,29160]).
horario('Path:11',202,[28320,28800,29040,29280,30060]).
horario('Path:11',204,[29220,29700,29940,30180,30960]).
horario('Path:11',205,[30120,30600,30840,31080,31860]).
horario('Path:11',206,[31020,31500,31740,31980,32760]).
horario('Path:11',207,[32820,33300,33540,33780,34560]).
horario('Path:11',208,[33720,34200,34440,34680,35460]).
horario('Path:11',209,[34620,35100,35340,35580,36360]).
horario('Path:35',255,[73200,73320]).
horario('Path:35',256,[74520,74640]).
horario('Path:35',257,[75840,75960]).
horario('Path:36',344,[27300,27600]).
horario('Path:36',345,[28500,28800]).
horario('Path:36',346,[29700,30000]).
horario('Path:36',347,[80000,80300]).
horario('Path:22',111,[25400,25600,25800,26000,26200,26400]).
horario('Path:22',112,[80400,80450,80500,80550,80600,80650]).
horario('Path:22',113,[82000,82050,82100,82150,82200,82250]).
horario('Path:24',514,[28200,28400,28600,28800,29000,29200]).
horario('Path:24',515,[30660,30960,31200,31440,31680,31980]).
horario('Path:24',516,[75000,75200,75400,75600,75800,76000]).
horario('Path:34',622,[26580,26700]).
horario('Path:34',623,[27900,28020]).
horario('Path:34',624,[29220,29340]).
horario('Path:26',75,[28320,28620,28860,29100,29340,29640]).
horario('Path:26',76,[29640,29940,30180,30420,30660,30960]).
horario('Path:26',78,[30960,31260,31500,31740,31980,32280]).
horario('Path:26',79,[32280,32580,32820,33060,33300,33600]).
horario('Path:26',80,[33600,33900,34140,34380,34620,34920]).
horario('Path:20',743,[28980,29280,29520,29760,30120,30360]).
horario('Path:20',744,[30180,30480,30720,30960,31320,31560]).
horario('Path:20',745,[31380,31680,31920,32160,32520,32760]).
horario('Path:20',746,[32580,32880,33120,33360,33720,33960]).
horario('Path:20',747,[33780,34080,34320,34560,34920,35160]).
horario('Path:9',800,[29160,29940,30180,30420,30900]).
horario('Path:9',801,[30060,30840,31080,31320,31800]).
horario('Path:9',802,[30960,31740,31980,32220,32700]).
horario('Path:9',803,[31860,32640,32880,33120,33600]).
horario('Path:9',804,[32760,33540,33780,34020,34500]).
horario('Path:9',805,[33660,34440,34680,34920,35400]).
horario('Path:39',900,[74520,74640]).
horario('Path:39',901,[75900,76020]).
horario('Path:39',902,[76320,76440]).
horario('Path:39',903,[76800,76920]).
horario('Path:39',904,[77220,77340]).
horario('Path:39',905,[77700,77820]).
horario('Path:39',906,[82320,82440]).
horario('Path:39',907,[85800,85920]).
horario('Path:39',908,[89820,89940]).
horario('Path:37',188,[74760,75060]).
horario('Path:37',189,[75960,76260]).
horario('Path:37',190,[77160,77460]).
horario('Path:45',297,[28320,28620,28860,29100,29340,29640]).
horario('Path:45',298,[29640,29940,30180,30420,30660,30960]).
horario('Path:45',299,[30960,31260,31500,31740,31980,32280]).
horario('Path:45',300,[32280,32580,32820,33060,33300,33600]).
horario('Path:45',301,[33600,33900,34140,34380,34620,34920]).
horario('Path:88',655,[28320,28620,28860,29100,29340,29640]).
horario('Path:88',656,[29640,29940,30180,30420,30660,30960]).
horario('Path:88',657,[30960,31260,31500,31740,31980,32280]).
horario('Path:88',658,[32280,32580,32820,33060,33300,33600]).
horario('Path:88',659,[33600,33900,34140,34380,34620,34920]).
horario('Path:90',1001,[28320,28620,28860,29100,29340,29640]).
horario('Path:90',1002,[29640,29940,30180,30420,30660,30960]).
horario('Path:90',1003,[30960,31260,31500,31740,31980,32280]).
horario('Path:90',1004,[32280,32580,32820,33060,33300,33600]).
horario('Path:90',1005,[33600,33900,34140,34380,34620,34920]).
horario('Path:3',31,[37800,38280,38580,38880,39420]).
horario('Path:1',63,[39600,40140,40440,40740,41220]).
horario('Path:3',33,[41400,41880,42180,42480,43020]).
horario('Path:1',65,[43200,43740,44040,44340,44820]).
horario('Path:3',35,[45000,45480,45780,46080,46620]).
horario('Path:1',67,[46800,47340,47640,47940,48420]).
horario('Path:3',37,[48600,49080,49380,49680,50220]).
horario('Path:1',69,[50400,50940,51240,51540,52020]).
horario('Path:3',39,[52200,52680,52980,53280,53820]).
horario('Path:1',71,[54000,54540,54840,55140,55620]).
horario('Path:3',41,[55800,56280,56580,56880,57420]).
horario('Path:1',73,[57600,58140,58440,58740,59220]).
horario('Path:3',43,[59400,59880,60180,60480,61020]).
horario('Path:1',75,[61200,61740,62040,62340,62820]).
horario('Path:3',45,[63000,63480,63780,64080,64620]).
horario('Path:1',77,[64800,65340,65640,65940,66420]).
horario('Path:3',48,[66600,67080,67380,67680,68220]).
horario('Path:1',82,[68400,68940,69240,69540,70020]).
horario('Path:3',52,[70200,70680,70980,71280,71820]).
horario('Path:1',86,[72000,72540,72840,73140,73620]).
horario('Path:3',56,[73800,74280,74580,74880,75420]).
horario('Path:1',432,[75600,76140,76440,76740,77220]).
horario('Path:39',460,[77220,77340]).
horario('Path:3',46,[63900,64380,64680,64980,65520]).
horario('Path:1',79,[65700,66240,66540,66840,67320]).
horario('Path:3',49,[67500,67980,68280,68580,69120]).
horario('Path:1',83,[69300,69840,70140,70440,70920]).
horario('Path:3',53,[71100,71580,71880,72180,72720]).
horario('Path:1',436,[72900,73440,73740,74040,74520]).
horario('Path:11',128,[67920,68400,68640,68880,69660]).
horario('Path:9',157,[69660,70440,70680,70920,71400]).
horario('Path:11',132,[71520,72000,72240,72480,73260]).
horario('Path:9',161,[73260,74040,74280,74520,75000]).
horario('Path:11',136,[75120,75600,75840,76080,76860]).
horario('Path:9',165,[76860,77640,77880,78120,78600]).
horario('Path:11',137,[78720,79200,79440,79680,80460]).
horario('Path:9',166,[80460,81240,81480,81720,82200]).
horario('Path:11',138,[82320,82800,83040,83280,84060]).
horario('Path:9',167,[84060,84840,85080,85320,85800]).
horario('Path:11',130,[69720,70200,70440,70680,71460]).
horario('Path:9',159,[71460,72240,72480,72720,73200]).
horario('Path:11',134,[73320,73800,74040,74280,75060]).
horario('Path:9',163,[75060,75840,76080,76320,76800]).
horario('Path:38',459,[34080,34200]).
horario('Path:3',29,[34200,34680,34980,35280,35820]).
horario('Path:1',61,[36000,36540,36840,37140,37620]).

% workblock(WorkBlock, List_of_Trips,StartTime, EndTime)
%VD12
workblock(12,[459,29,61],34080,37620).
workblock(211,[31,63],37620,41220).
workblock(212,[33,65],41220,44820).
workblock(213,[35,67],44820,48420).
workblock(214,[37,69],48420,52020).
workblock(215,[39,71],52020,55620).
workblock(216,[41,73],55620,59220).
workblock(217,[43,75],59220,62820).
workblock(218,[45,77],62820,66420).
workblock(219,[48,82],66420,70020).
workblock(220,[52,86],70020,73620).
workblock(221,[56,432],73620,77220).
workblock(222,[460],77220,77340).

%VD13
workblock(13,[461,46,79],63780,67320).
workblock(223,[49,83],67320,70920).
workblock(224,[53,436],70920,74520).
workblock(225,[462],74520,74640).

%VD14
workblock(14,[463,128],67800,69660).
workblock(226,[157],69660,71520).
workblock(227,[132,161],71520,75000).
workblock(228,[136],75000,76860).
workblock(229,[165],76860,78720).
workblock(230,[137,166],78720,82200).
workblock(231,[138],82200,84060).
workblock(232,[167,464],84060,85920).

%VD15
workblock(15,[465,130],69600,71460).
workblock(233,[159],71460,73320).
workblock(234,[134,163],73320,76800).
workblock(235,[466],76800,76920).

%vehicleDuty(Id,WorkBlocks)
vehicleDuty(12,[12,211,212,213,214,215,216,217,218,219,220,221,222]).
vehicleDuty(13,[13,223,224,225]).
vehicleDuty(14,[14,226,227,228,229,230,231,232]).
vehicleDuty(15,[15,233,234,235]).

%rangevd(VehicleDuty, StartTime, EndTime)
rangevd(12,34080,77340).
rangevd(13,63780,74640).
rangevd(14,67800,85920).
rangevd(15,69600,76920).

% horariomotorista(Driver,WorkStart,WorkEnd,WorkTime,DuracaoBlocosMotorista)
%                         7      17	8	6     2
horariomotorista(276,   25200, 61200, 28800, [21600,7200]).
horariomotorista(527,   25200, 61200, 28800, [21600,7200]).
horariomotorista(889,	25200, 61200, 28800, [21600,7200]).
horariomotorista(1055,  25200, 61200, 28800, [14400,14400]).
horariomotorista(1461,  25200, 61200, 28800, [14400,14400]).
horariomotorista(1640,  25200, 61200, 28800, [21600,7200]).
horariomotorista(2049,  25200, 61200, 28800, [21600,7200]).
horariomotorista(5188,  33300, 69300, 28800, [7200, 21600]).
horariomotorista(6616,  33300, 69300, 28800, [14400,14400]).
horariomotorista(6697,  33300, 69300, 28800, [21600,7200]).
horariomotorista(11018, 41400, 77400, 28800, [21600,7200]).
horariomotorista(11692, 41400, 77400, 28800, [21600,7200]).
horariomotorista(14893, 45000, 81000, 28800, [10800,18000]).
horariomotorista(16458, 50400, 86400, 28800, [14400,14400]).
horariomotorista(16690, 50400, 86400, 28800, [7200, 21600]).
horariomotorista(16763, 50400, 86400, 28800, [14400,14400]).
horariomotorista(17015, 50400, 86400, 28800, [10800,18000]).
horariomotorista(17552, 54000, 90000, 28800, [10800,18000]).
horariomotorista(17623, 25200, 61200, 28800, [21600,7200]).
horariomotorista(17630, 25200, 61200, 28800, [21600,7200]).
horariomotorista(17639, 27000, 48600, 21600, [21600]).
horariomotorista(17673, 25200, 61200, 28800, [21600,7200]).
horariomotorista(18009, 50400, 86400, 28800, [7200, 21600]).
horariomotorista(18050, 54000, 90000, 28800, [21600,7200]).
horariomotorista(18097, 57600, 79200, 21600, [21600]).
horariomotorista(18105, 57600, 79200, 21600, [21600]).
horariomotorista(18107, 57600, 79200, 21600, [21600]).
horariomotorista(18119, 59400, 81000, 21600, [21600]).
horariomotorista(18131, 66600, 88200, 21600, [21600]).


% lista_motoristas_nworkblocks(12,[(276,2),(5188,3),(16690,2),(18107,6)]).

%Parameterizacao
margem(0.15).

% afetacao(Vd,D,Nwb)
:-dynamic afetacao/3.
:-dynamic lista_motoristas_nworkblocks/2.

escalonamento():-
	verifica_margem(),
	findall(_,blocos_motorista(),_),
	afetar_motoristas(),
	findall(X,rangevd(X,_,_),Vduties),
	criar_lista_mot_nwb(Vduties),
	findall((D,L),lista_motoristas_nworkblocks(D,L),MotWb),
	gera_corrigidos(MotWb).

gera_corrigidos([]):-!.
gera_corrigidos([(Vd,Mw)|MotWb]):-
	gera((Vd,Mw),[MPop*V|_]),!,
	(V>0,!,
	 vehicleDuty(Vd,Wb),
	 corrige_quatro_horas_seguidas(Vd,MPop,Wb,C),
	((MPop\==C,write('VehicleDuty '),write(Vd),write(' alterado: '),write(C),nl),!;write('VehicleDuty '),write(Vd),write(': '),write(MPop),nl)),

	write('_________________________________________________________________________________________'),nl,nl,
	gera_corrigidos(MotWb).

corrige_quatro_horas_seguidas(_,[I],[_],[I]):-!.
corrige_quatro_horas_seguidas(Vd,[I|Pop],[W|Wb],[X|Pop2]):-
	horas_seguidas([I|Pop],[W|Wb],H),
	%4h=14400s
	H>14400,
        workblock(W,_,St,Et),
	findall(t(Tst,Tet,Time,D),t(Tst,Tet,Time,D),T),
	novo_condutor(St,Et,T,X),
	alteracao_quatro_horas(Vd,St,Et,I,X),
	corrige_quatro_horas_seguidas(Vd,Pop,Wb,Pop2),!.
corrige_quatro_horas_seguidas(Vd,[I|Pop],[_|Wb],[I|Pop2]):-
	corrige_quatro_horas_seguidas(Vd,Pop,Wb,Pop2).

alteracao_quatro_horas(Vd,St,Et,D,X):-
	write('(VehicleDuty '),write(Vd),write(') Alteracao do condutor '),write(D), write(' para o condutor '),write(X),write('.['),write(St),write('-'),write(Et),write(', 4h]'),nl,nl.

horas_seguidas([I|Pop],[W|Wb],H):-
	workblock(W,_,St,Et),
	N1 is Et-St,
	horas_seguidas(I,Pop,Wb,N1,H).

horas_seguidas(I,[I2|Pop],[W|Wb],N,H):-
	I==I2,
	workblock(W,_,St,Et),
	N1 is N+(Et-St),
	horas_seguidas(I,Pop,Wb,N1,H),!.
horas_seguidas(_,_,_,N,N):-!.


novo_condutor(_,_,[],_):-false,!.
novo_condutor(WbSt,WbEt,[t(St,Et,Time,X)|_],X):-
	St=<WbSt, Et>=WbEt,
	retract(t(St,Et,Time,X)),
	((St\==WbSt,Et\==WbEt,
	 T1 is WbSt-St,
	 T2 is Et-WbEt,
	assert(t(St,WbSt,T1,X)),assert(t(WbEt,Et,T2,X)));true),!.
novo_condutor(WbSt,WbEt,[_|Tuples],X):-
	novo_condutor(WbSt,WbEt,Tuples,X).

criar_lista_mot_nwb([]):-!.
criar_lista_mot_nwb([Vd|V]):-
	findall(Nwb,afetacao(Vd,_,Nwb),LNwb),
	somar_lista(LNwb,Totalwb),
	vehicleDuty(Vd,Lvd),
	length(Lvd,VdTotalwb),
	findall((D,Nwb),afetacao(Vd,D,Nwb),Afetacoes),
	%verifica o nr de workblock e adiciona os workblocks em falta ao ultimo driver
	((Totalwb<VdTotalwb,
	       Dif is VdTotalwb-Totalwb,
	       reverse(Afetacoes,[(D,N)|A]),
	       N1 is N+Dif,
	       reverse([(D,N1)|A],Afetacoes1),
	       assert(lista_motoristas_nworkblocks(Vd,Afetacoes1)),!);

	assert(lista_motoristas_nworkblocks(Vd,Afetacoes))),
	criar_lista_mot_nwb(V).

afetar_motoristas():-
	vd_inicio_crescente(V),
	tuples_inicio_crescente(T),
	afeta(V,T).

afeta([],_):-!.
afeta([(St,Et,Vd)|V],T):-
	vehicleDuty(Vd,Wb),
	maior_workblock(Wb,Twb),
	afeta_tuple(Vd,St,Et,Twb,T),
	afeta(V,T).

afeta_tuple(Vd,St,Et,_,[]):-write('Vehicle Duty: '),write(Vd),write(' não foi totalmente afetado. Faltam condutores entre os horários '),write(St),write(' e '),write(Et),write('.'),nl,!.
afeta_tuple(_,Et,Et,_,_):-!.
afeta_tuple(Vd,St,Et,Twb,[(Tst,Tet,Ttime,D)|T]):-
	((Tst=<St, Tet>St, Tet<Et),
	     (Twork is Tet-St,
	      Nwb is truncate(Twork/Twb),
	      Nwb>0,
	      Fim is St+Twb*Nwb,
	      Tsobra1 is St-Tst,
	      Tsobra2 is Tet-Fim,
	      retract(t(Tst,Tet,Ttime,D)),
	      ((Tsobra1>0,assert(t(Tst,St,Tsobra1,D)));true),
	      ((Tsobra2>0,assert(t(Fim,Tet,Tsobra2,D)));true),
	      assert(afetacao(Vd,D,Nwb)),
	      afeta_tuple(Vd,Fim,Et,Twb,T),!));

	((Tst<St,Tet>Et),
	      (Twork is Et-St,
	       Nwb is truncate(Twork/Twb),
	       Nwb>0,
	       retract(t(Tst,Tet,Ttime,D)),
	       assert(afetacao(Vd,D,Nwb)),
	       afeta_tuple(Vd,Et,Et,Twb,T),!));

	((Tst>St,Tst<Et, Tet>=Et),
	      (Twork is Et-Tst,
	       Nwb is truncate(Twork/Twb),
	       Nwb>0,
	       Fim is Tst+Twb*Nwb,
	       Tsobra is Tet-Et,
	       retract(t(Tst,Tet,Ttime,D)),
	       ((Tsobra>0,assert(t(Fim,Tet,Tsobra,D)));true),
	       assert(afetacao(Vd,D,Nwb)),
	       afeta_tuple(Vd,St,Tst,Twb,T),!)).
afeta_tuple(Vd,St,Et,Twb,[_|T]):-
	afeta_tuple(Vd,St,Et,Twb,T).

maior_workblock(Wb,T):-
	maior_workblock(Wb,0,T).

maior_workblock([],Twb,Twb):-!.
maior_workblock([H|Wb],Twb,Tf):-
	workblock(H,_,St,Et),
	T is Et-St,
	T>Twb,
	maior_workblock(Wb,T,Tf),!.
maior_workblock([_|Wb],Twb,Tf):-
	maior_workblock(Wb,Twb,Tf).

% ListaOrd= [(StartTime,VehicleDuty)|...]
vd_inicio_crescente(ListaOrd):-
	findall((S,E,V),rangevd(V,S,E),Lista),
	sort(Lista, ListaOrd).

tuples_inicio_crescente(ListaOrd):-
	findall((St,Et,Time,D),t(St,Et,Time,D),Lista),
	sort(Lista,ListaOrd).

%t(StartTime,EndTime,WorkTime,Driver)
:-dynamic t/4.
blocos_motorista():-
	horariomotorista(M,StartTime,EndTime,WorkTime,Blocks),
	T is (EndTime-StartTime)-WorkTime,
	length(Blocks,L),
	T1 is T/L,
	criar_blocos(M,StartTime,EndTime,T1,Blocks,L).

criar_blocos(M,_,EndTime,T,[H|_],1):-
	St is EndTime-H-T,
	assert(t(St,EndTime,H,M)),!.
criar_blocos(M,StartTime,EndTime,T,[H|Blocks],L):-
	Et is StartTime+H+T,
	assert(t(StartTime,Et,H,M)),
	L1 is L-1,
	criar_blocos(M,Et,EndTime,T,Blocks,L1).


verifica_margem():-
	carga_total(Carga),
	capacidade_sistema(Capacidade),
	(Capacidade>Carga,!;
	write('Capacidade menor que a carga'),nl,false),
	margem(Margem),
	M is 1-(Carga/Capacidade),
	(M>Margem,!;
	write('Margem abaixo do desejável'),nl,
	false).

capacidade_sistema(CS):-
	findall(X,horariomotorista(_,_,_,X,_),C),
	somar_lista(C,CS).

carga_total(C):-
	findall(X,ranges(X),R),
	somar_lista(R,C).

somar_lista([],0):-!.
somar_lista([H|R],C):-
	somar_lista(R,C1),
	C is  C1 + H.

ranges(R):-
	rangevd(_,S,E),
	R is E-S.



%               ///////////////////////////////////////////////
%              //////////////                 ////////////////
%             //////////////  /////////////  ////////////////
%            //////////////  ///            ////////////////
%           //////////////  ///	           ////////////////
%          //////////////  /////////////  ////////////////
%	  //////////////                 ////////////////
%        ///////////////////////////////////////////////

% parameterizacao
geracoes(5).
populacao(4).
prob_cruzamento(0.5).
prob_mutacao(0.4).
target(-1).
tempo(5000).
geracoes_repetidas(10).

per_individuo(0.7).
peso_hard_constraint1(10).
peso_hard_constraint2(8).
peso_soft_constraint(1).

:-dynamic melhor/1.
:-dynamic geracoes/1.
:-dynamic populacao/1.
:-dynamic prob_cruzamento/1.
:-dynamic prob_mutacao/1.
:-dynamic target/1.
:-dynamic geracoes_repetidas/1.

gera((Vd,MotWb),PopOrd):-
	gera_populacao((Vd,MotWb),Pop),!,
	avalia_populacao(Pop,PopAv),
	ordena_populacao(PopAv,PopOrd),
	geracoes(NG),
	get_time(TempInit),
	gera_geracao((Vd,MotWb),0,TempInit,0,NG,PopOrd).

%Cria uma lista com os condutores
gera_condutores((_,[(H,Num)|Lista]),LMaisFinal):-
	gera_condutores2(H,Num,Lista,LFinal),
	random_permutation(LFinal,LMaisFinal),
	!.

gera_condutores2(_,0,[],[]):-!.

gera_condutores2(H,N,L,[H|LFinal]):-
	N \= 0,
	N1 is N-1,
	gera_condutores2(H,N1,L,LFinal).

gera_condutores2(_,0,[(H,Num)|L],LFinal):-
	gera_condutores2(H,Num,L,LFinal).


gera_populacao(MotWb,Pop):-
	populacao(TamPop),
	gera_condutores(MotWb,X),
	length(X,N),
	gera_populacao(TamPop,X,N,Pop).

gera_populacao(0,_,_,[]):-!.

gera_populacao(TamPop,Lista,NumT,[Ind|Resto]):-
	TamPop1 is TamPop-1,
	gera_populacao(TamPop1,Lista,NumT,Resto),
	gera_individuo(Lista,NumT,Ind).
	%not(member(Ind,Resto)).
gera_populacao(TamPop,Lista,NumT,L):-
	gera_populacao(TamPop,Lista,NumT,L).

gera_individuo([G],1,[G]):-!.

gera_individuo(Lista,NumT,[G|Resto]):-
	NumTemp is NumT + 1, % To use with random
	random(1,NumTemp,N),
	retira(N,Lista,G,NovaLista),
	NumT1 is NumT-1,
	gera_individuo(NovaLista,NumT1,Resto).

retira(1,[G|Resto],G,Resto).
retira(N,[G1|Resto],G,[G1|Resto1]):-
	N1 is N-1,
	retira(N1,Resto,G,Resto1).

avalia_populacao([],[]).
avalia_populacao([Ind|Resto],[Ind*V|Resto1]):-
	agenda(Ind),
	pausas,
	avalia(V),
	retractall(t(_,_,_)),retractall(p(_,_,_)),
	avalia_populacao(Resto,Resto1).

avalia(V1):-
	findall(X,avalia_pausas_refeicoes(X),PPausas),
	findall(Y,avalia_tempos_trabalho(Y),PTrabalhos),
	append(PPausas,PTrabalhos,Pesos),
	somar_lista(Pesos,V1),
	retractall(visitado(_)).

:-dynamic visitado/1.
avalia_tempos_trabalho(Vf):-
	peso_hard_constraint1(P),
	t(Hi,Hf,I),
	avalia_quatro_horas_seguidas(Hi,Hf,P,V),
	avalia_oito_horas_totais(I,P,V,V2),
	verifica_preferencia_horario(I,V2,Vf).

verifica_preferencia_horario(I,V2,Vf):-
	findall((Hi,Hf),t(Hi,Hf,I),Horarios),
	length(Horarios,L),
	verifica_horario(L,L,Horarios,V3),
	Vf is V2+V3.

%Preferencias: 10h=36000 20h=72000
verifica_horario(0,_,[],0):-!.
verifica_horario(L,C,[(Hi,_)|Horarios],V):-
	peso_soft_constraint(P),
	L==C,
	L1 is L-1,
	verifica_horario(L1,C,Horarios,Vf),
	((Hi<36000, V is Vf+(36000-Hi)*P);V is Vf),!.
verifica_horario(1,_,[(_,Hf)],V):-
	peso_soft_constraint(P),
	verifica_horario(0,_,[],Vf),
	((Hf>72000, V is Vf+(Hf-72000)*P);V is Vf),!.

verifica_horario(L,C,[_|Horarios],V):-
	L1 is L-1,
	verifica_horario(L1,C,Horarios,V).


avalia_oito_horas_totais(I,P,V,Vf):-
	%verifica se ja avaliou o motorista I
	findall(X,visitado(X),Visitados),
	\+member(I,Visitados),
	findall((Hi,Hf),t(Hi,Hf,I),Horarios),
	soma_horarios(Horarios,S),
	%8h=28800
	S>28800,
	Vf is V+(S-28800)*P,
	assert(visitado(I)),!.
avalia_oito_horas_totais(_,_,V,V).


soma_horarios([],0):-!.
soma_horarios([(Hi,Hf)|Horarios],S):-
	soma_horarios(Horarios,S1),
	S is S1+(Hf-Hi).

%4h=14400
avalia_quatro_horas_seguidas(Hi,Hf,P,V):-
	Hf-Hi>14400,
	V is ((Hf-Hi)-14400)*P,!.
avalia_quatro_horas_seguidas(_,_,_,0).

avalia_pausas_refeicoes(V2):-
	p(Hi,Hf,_),
	%11h=39600 15h=54000
	pausa_refeicao(39600,54000,Hi,Hf,0,V1),
	%18h=64800 22h=79200
	pausa_refeicao(64800,79200,Hi,Hf,V1,V2).

pausa_refeicao(Refi,Reff,Hi,Hf,V,Vf):-
	%1h=3600
	peso_hard_constraint2(P),
	%quando a pausa comeca antes da referencia inicial e acaba entre as ambas
	(((Hi<Refi, Hf>Refi), ((Hf-Refi>=3600,Vf is 0); Vf is V+(Hf-Refi)*P,!));

	%quando a pausa esta entre as duas referencias
	((Hi>Refi, Hf<Reff), ((Hf-Hi>=3600,Vf is 0); Vf is V+(Hf-Hi)*P,!));

	%quando a pausa comeca entre as referencias e acaba depois da final
	((Hi<Reff, Hf>Reff), ((Reff-Hi>=3600,Vf is 0); Vf is V+(Reff-Hi)*P,!))),!.
pausa_refeicao(_,_,_,_,V,V).


:-dynamic p/3.
pausas:-
	t(_,Tf,I),!,
	pausa(Tf,I).

%pausas:-!.


pausa(Tf1,I):-
	t(Tf1,Tf2,I2),
	(percorre(Tf2,I,Tf1);true),
	(pausa(Tf2,I2);true).
pausa(_,_):-!.

percorre(Tf,I,Tfi):-
	t(Tf,_,I2),
	I2==I,
	assert(p(Tfi,Tf,I)),!.
percorre(Tf,I,Tfi):-
	t(Tf,Tf2,_),
	percorre(Tf2,I,Tfi),!.

agenda(Ind):-
	vehicleDuty(_,WorkBlocks),
	length(Ind,L),
	agenda2(L,WorkBlocks,Ind,Agenda),
	length(Agenda,LA),
	agenda3(LA,Agenda,0).

agenda2(0,_,_,[]):-!.
agenda2(L,[W|WorkBlocks],[I|Ind],[(HInicio,HFim,I)|Agenda]):-
	workblock(W,_,HInicio,HFim),
	L1 is L-1,
	agenda2(L1,WorkBlocks,Ind,Agenda).

:-dynamic t/3.
agenda3(0,_,_):-!.
agenda3(LA,[(Hi,Hf,I)|Agenda],I2):-
	t(Hi2,Hi,I2),
	I==I2,
	retract(t(Hi2,_,I)),!,
	assert(t(Hi2,Hf,I)),
	L is LA-1,
	agenda3(L,Agenda,I).

agenda3(LA,[(Hi,Hf,I)|Agenda],_):-
	assert(t(Hi,Hf,I)),
	L is LA-1,
	agenda3(L,Agenda,I).



ordena_populacao(PopAv,PopAvOrd):-
	bsort(PopAv,PopAvOrd).

bsort([X],[X]):-!.
bsort([X|Xs],Ys):-
	bsort(Xs,Zs),
	btroca([X|Zs],Ys).


btroca([X],[X]):-!.

btroca([X*VX,Y*VY|L1],[Y*VY|L2]):-
	VX>VY,!,
	btroca([X*VX|L1],L2).

btroca([X|L1],[X|L2]):-btroca(L1,L2).

menorAvaliacao([Pop*Eva|_]):-
	(retractall(melhor(_*_));true), asserta(melhor(Pop*Eva)),!.

gera_geracao(_,G,_,_,G,Pop):-
	%write('Geracao '), write(G), write(':'), nl, write(Pop), nl,
        menorAvaliacao(Pop),!.

gera_geracao(_,G,_,N,_,Pop):-
	%write('Geracao '), write(G), write(':'), nl, write(Pop), nl,
	geracoes_repetidas(GR),
	GR==N,
        menorAvaliacao(Pop),!,
	write('Estabilizacao de geracoes('),write(N),write(')'), nl,!.


gera_geracao(_,_,TempInit,_,_,Pop):-
	tempo(Var),
	get_time(TempoAtual),TempoAtual-TempInit > Var,
	write('TEMPO: '), write(TempInit),nl, write('ATUAL: '),write(TempoAtual), nl, write('Paragem por tempo limite('),write(Var),write(')'), nl,
        menorAvaliacao(Pop),!.

gera_geracao(_,_,_,_,_,[Pop*V|T]):-
	target(Z),
	%write(Z),write('<='),write(V),nl,
	Z >= V,
        write('Paragem por valor menor ou igual ao Target('),write(Z),write(')'),nl,
        menorAvaliacao([Pop*V|T]),!.

gera_geracao((Vd,MotWb),N,TempInit,Count,G,Pop):-
%	write('Geracao '), write(N), write(':'), nl, write(Pop), nl,

	%aleatoridade dos individuos da lista
	random_permutation(Pop,RPop),

	cruzamento((Vd,MotWb),RPop,NPop1),
	mutacao((Vd,MotWb),NPop1,NPop),
	avalia_populacao(NPop,NPopAv),
	ordena_populacao(NPopAv,NPopOrd),

	%write('filhos='),write(NPopOrd),nl,nl,

	%junta as duas geracoes
	append(Pop,NPopOrd,PopTotal),
	ordena_populacao(PopTotal,OrdPopTotal),

	populacao(NG),
	%funcao que vai buscar a primeira melhor resposta e adiciona os restantes(10% de hipoteses)
	obter_individuos(NG,OrdPopTotal,MPopTotal,PopMaSorte),

	%preenche o resto da lista se faltarem elementos
	length(MPopTotal,T),
	preencher_lista(T,MPopTotal,PopMaSorte,PopFinal),
	(   ( PopFinal == Pop ,Count1 is Count+1 )
	;
	(  PopFinal \== Pop , Count1 is 0)),
	%write('Repetidos: '),write(Count1),nl,

	ordena_populacao(PopFinal, PopMaisQueFinal),
	N1 is N+1,
	gera_geracao((Vd,MotWb),N1,TempInit,Count1,G,PopMaisQueFinal).

obter_individuos(NG,[H|NPopOrd],[H|MPopOrd],PopMaSorte):-
	NG1 is NG-1,
	nao_elitista(NG1,NPopOrd,MPopOrd,H,PopMaSorte).

nao_elitista(0,_,[],_,[]):-!.
nao_elitista(NG,[H|NPopOrd],[H|MPopOrd],T,PopMaSorte):-
	random(0.0,1.0,N),
	per_individuo(P),
	N<P,
	H\==T,
	NG1 is NG-1,
        nao_elitista(NG1,NPopOrd,MPopOrd,T,PopMaSorte),
	\+member(H,MPopOrd),!.
nao_elitista(NG,[H|NPopOrd],MPopOrd,_0,[H|PopMaSorte]):-
	NG1 is NG-1,
	nao_elitista(NG1,NPopOrd,MPopOrd,_,PopMaSorte),!.

preencher_lista(Tlista,PopSorte,PopSorte,_):-
	populacao(P),
	Tlista==P,!.
preencher_lista(_,MPopTotal,PopMaSorte,PopFinal):-
	append(MPopTotal,PopMaSorte,PopFinal).

gerar_pontos_cruzamento(MotWb,P1,P2):-
	gerar_pontos_cruzamento1(MotWb,P1,P2).

gerar_pontos_cruzamento1((Vd,_),P1,P2):-
	vehicleDuty(Vd,Wb),
	length(Wb,N),
	NTemp is N+1,
	random(1,NTemp,P11),
	random(1,NTemp,P21),
	P11\==P21,!,
	((P11<P21,!,P1=P11,P2=P21);P1=P21,P2=P11).
gerar_pontos_cruzamento1((Vd,_),P1,P2):-
	gerar_pontos_cruzamento1((Vd,_),P1,P2).


cruzamento(_,[],[]):-!.

cruzamento(_,[Ind*_],[Ind]):-!.

cruzamento(MotWb,[Ind1*_,Ind2*_|Resto],[NInd1,NInd2|Resto1]):-
	gerar_pontos_cruzamento(MotWb,P1,P2),
	prob_cruzamento(Pcruz),	random(0.0,1.0,Pc),
	((Pc =< Pcruz,!,
        cruzar(MotWb,Ind1,Ind2,P1,P2,NInd1),
	  cruzar(MotWb,Ind2,Ind1,P1,P2,NInd2))
	;
	(NInd1=Ind1,NInd2=Ind2)),
	cruzamento(MotWb,Resto,Resto1).


cruzar((Vd,L),Ind1,Ind2,P1,P2,NInd11):-
	sublista(Ind1,P1,P2,Sub1),
	vehicleDuty(Vd,Wb),
	length(Wb,NumT),
	R is NumT-P2,
	rotate_right(NumT,Ind2,R,Ind21),
	P3 is P2 + 1,
	insere(NumT,Ind21,Sub1,P3,L,NInd1),
	eliminah(NInd1,NInd11).


preencheh([],[]):-!.

preencheh([_|R1],[h|R2]):-
	preencheh(R1,R2).


sublista(L1,I1,I2,L):-
	I1 < I2,!,
	sublista1(L1,I1,I2,L).

sublista(L1,I1,I2,L):-
	sublista1(L1,I2,I1,L).

sublista1([X|R1],1,1,[X|H]):-!,
	preencheh(R1,H).

sublista1([X|R1],1,N2,[X|R2]):-!,
	N3 is N2 - 1,
	sublista1(R1,1,N3,R2).

sublista1([_|R1],N1,N2,[h|R2]):-
	N3 is N1 - 1,
	N4 is N2 - 1,
	sublista1(R1,N3,N4,R2).

rotate_right(N,L,K,L1):-
	T is N - K,
	rr(T,L,L1).

rr(0,L,L):-!.

rr(N,[X|R],R2):-
	N1 is N - 1,
	append(R,[X],R1),
	rr(N1,R1,R2).

insere(_,[],L,_,_,L):-!.
insere(T,[X|R],L,N,LMot,L2):-
	((N>T,!,N1 is N mod T);N1 = N),
	nth0(_,LMot,(X,Rep)),
	vezes_repetidas_lista(X,L,RepLista),

(
	    (Rep>RepLista,
	    insere1(X,N1,L,L1),
	    N2 is N + 1,
	    insere(T,R,L1,N2,LMot,L2))
	;
	    (insere(T,R,L,N,LMot,L2))
	),!.

insere1(X,1,L,[X|L]):-!.
insere1(X,N,[Y|L],[Y|L1]):-
	N1 is N-1,
	insere1(X,N1,L,L1).

vezes_repetidas_lista(_,[],0):-!.
vezes_repetidas_lista(X,[X|L],RepLista):-
	vezes_repetidas_lista(X,L,R),
	RepLista is R+1,!.
vezes_repetidas_lista(X,[_|L],RepLista):-
	vezes_repetidas_lista(X,L,RepLista).

eliminah([],[]):-!.

eliminah([h|R1],R2):-!,
	eliminah(R1,R2).

eliminah([X|R1],[X|R2]):-
	eliminah(R1,R2).

mutacao(_,[],[]):-!.
mutacao(VdMotWb,[Ind|Rest],[NInd|Rest1]):-
	prob_mutacao(Pmut),
	random(0.0,1.0,Pm),
	((Pm < Pmut,!,mutacao1(VdMotWb,Ind,NInd));NInd = Ind),
	mutacao(VdMotWb,Rest,Rest1).

mutacao1(VdMotWb,Ind,NInd):-
	gerar_pontos_cruzamento(VdMotWb,P1,P2),
	mutacao22(Ind,P1,P2,NInd).

mutacao22([G1|Ind],1,P2,[G2|NInd]):-
	!, P21 is P2-1,
	mutacao23(G1,P21,Ind,G2,NInd).
mutacao22([G|Ind],P1,P2,[G|NInd]):-
	P11 is P1-1, P21 is P2-1,
	mutacao22(Ind,P11,P21,NInd).

mutacao23(G1,1,[G2|Ind],G2,[G1|Ind]):-!.
mutacao23(G1,P,[G|Ind],G2,[G|NInd]):-
	P1 is P-1,
	mutacao23(G1,P1,Ind,G2,NInd).

